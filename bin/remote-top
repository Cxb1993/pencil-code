#!/bin/csh

# Name:   remote-top
# Author: wd+ab
# Date:    7-Oct-2002
# $Id: remote-top,v 1.6 2004-02-05 23:31:51 dobler Exp $
# Description:
#     does top -bn1 on all procs of nodelist, write to datadir

set debug = 0

# Unpack NODELIST to csh array
set nodelist = `echo "$NODELIST" | sed 's/:/ /g'`

# Check for existence of `top' command:
set node = $nodelist[1]
set top_cmd = 'top -bn1 2>&1 > /dev/null; echo $?'
set top_stat = `$SSH $node "sh -c '$top_cmd'"`
if ("$top_stat" != "0") then         # return status should be 0
  echo "remote-top: Can't find top command on $node --- exiting"
  exit 1
endif

if ($#argv > 0) then
  if (($1 == "-v") || ($1 == "--verbose")) then
    set debug = 1
    shift
  endif
endif

set targetdir = $PENCIL_WORKDIR/data

if ($debug) then
  echo "targetdir = $targetdir"
  echo "nodelist = $nodelist"
endif

date
while (1)
  set i=0
  foreach node ($nodelist)
    if ($debug) echo "node=$node; targetdir=$targetdir"
    $SSH $node "top -bn1 >! $targetdir/proc$i/top.log"
    set i = `expr $i + 1`
    echo 'i=' $i
  end
  sleep 60

end

# End of file
