#!/bin/csh
#                       setup-src
#                      -----------
# Description:
#   Link files into a local src directory
#   Also link files from the bin directory ==> should be renamed
# CVS $Id: setup-src,v 1.18 2003-11-04 14:20:34 mee Exp $
# Usage:
#   setup-src  [src-dir]

if (! $?PENCIL_HOME) then
  echo "You need to set PENCIL_HOME; consider sourcing sourceme.{,c}sh"
  exit 0
endif

set src = ./src
if ($#argv > 0) set src = "$1"

#  ## Determine directories to link from
#  set bindir="No-such-directory"
#  set srcdir="No-such-directory"
#  foreach dir ( ../../.. ../..)
#    # Note: the _last_ matching directory will be used
#    if (-d $dir/bin) set bindir=$dir/bin
#    if (-d $dir/src) set srcdir=$dir/src
#  end
set bindir = $PENCIL_HOME/bin
set srcdir = $PENCIL_HOME/src

## Link csh scripts
foreach script (start.csh start_run.csh run.csh getconf.csh)
  if (-e $bindir/$script) then
    ln -s $bindir/$script .
  else
    echo "No such file: $bindir/$script"
  endif
end

## Make a link to a root Makefile to allow make from the run directory
ln -s $srcdir/Makefile.parent Makefile

## src subdirectory
if (-d $src) then
  echo "$src existing src directory will be used"
else
  mkdir $src
endif
#
cd $src
#
#  LINK all *.f90, *.f, and the Makefile to local src directory
#
set n=0
foreach file ($srcdir/*.f90 $srcdir/*.f $srcdir/*.c \
              $srcdir/Makefile.src $srcdir/.cvsignore)
  if (-e $file) then
    if (! -e $file:t) then
      ln -s $file .
    else
      # echo "$file:t already exists"
      set n=`expr $n + 1`
      set existf=$file:t
    endif
  else
    echo "No such file: $file"
  endif
end
#
#  COPY all *.local files into local src directory
#
set n=0
foreach file ($srcdir/*.local)
  if (-e $file) then
    if (! -e $file:t) then
      cp $file .
    else
      # echo "$file:t already exists"
      set n=`expr $n + 1`
      set existf=$file:t
    endif
  else
    echo "No such file: $file"
  endif
end
#
if ($n == 1) then
  echo "$existf already exists in $src"
endif
if ($n > 1) echo "$n files already exist in $src"
#
#  Generate Makefile
#
rm -f Makefile			# for old run directories
adapt-mkfile Makefile.src Makefile
rm -f makefile			# for old run directories
