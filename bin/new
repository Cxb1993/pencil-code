#!/bin/csh
#
#  $Id: new,v 1.14 2005-06-26 17:34:16 eos_merger_tony Exp $
#
#  This script uses mkdatadir which links the subdirectory data
#  to some data disk (which is machine dependent and therefore
#  you may want to adapt mkdatadir for your particular machine).
#
#  Usage:
#     new run1 run2
#  Alternative usage, if one is already in run1
#     new run2
#
if ($#argv == 2) then
  set olddir=$cwd/$1
  set newdir=$cwd/$2
else
  set olddir=$cwd
  cd ..
  set newdir=$cwd/$1
endif
#
#  save current working directory and make new run directory
#  and go there.
#
set parentdir=$cwd
echo "Parent directory: " $parentdir
mkdir $newdir
cd $newdir
set targetdir=$cwd
#
#  In the new run directory, call mkdatadir, and
#  write file with name of directory from which
#  the new one is generated
#
$PENCIL_HOME/bin/mkdatadir
(echo $olddir; /bin/date) >>! data/new_from.dir
#
#  make src dir and go there
#
mkdir src
cd src
#
#  use *.local files from previous src directory
#  and run setup-src
#
echo "old source directory: " $olddir/src
cp $olddir/src/*.local .
echo "Use *.local files from:" `pwd`; ls -l *.local
cd ..
#
#  if there exists a bin/setup-src.local file
#  it is being executed prior to setup-src
#  This gives the possibility to link your own routines
#  into src (setup-src below won't overwrite those)
#
if (-f $parentdir/bin/setup-src.local) $parentdir/bin/setup-src.local
#
#  continue with the usual setup procedure
#
$PENCIL_HOME/bin/setup-src
#
#  copy *.in and k.dat files into new directory
#  the k.dat file is actually only needed for forced
#  turbulence runs
#
cd $olddir
echo "Use *.in files from:" `pwd`; ls -l *.in
cp *.in $targetdir
if (-f k.dat) cp k.dat $targetdir
if (-f stratification.dat) cp stratification.dat $targetdir
#
