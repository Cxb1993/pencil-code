#!/bin/csh -v -x

# Name:   copy-snapshots
# Author: wd (Wolfgang.Dobler@kis.uni-freiburg.de)
# Date:   24-Sep-2002
# Description:
#   Copy snapshots VAR# from /scratch to $PBS_O_WORKDIR (from where the
#   code started).
#     Relies on PBS (and on tcsh)  and is needed on Horseshoe (the Odense
#   cluster).
#     Should better be a Perl script.

set debug = 0

if ($#argv > 0) then
  if (($1 == "-v") || ($1 == "--verbose")) then
    set debug = 1
    shift
  endif
endif

if ($#argv > 0) then
  set varfile = $1
endif

set targetdir = $PWD/data
set nodelist = (`echo $NODELIST | sed 's/:/ /'`) # unpack NODELIST

if ($debug) then
  echo "SCRATCH_DIR = $SCRATCH_DIR"
  echo "targetdir = $targetdir"
  echo "nodelist = $nodelist"
endif

if ($?varfile) then		# explicit filename given
  set i=0
  foreach node ($nodelist)

    echo 'work on node i='$i
    if ($debug) echo "node=$node; targetdir=$targetdir"
    $SSH $node "ls -ltd $SCRATCH_DIR $SCRATCH_DIR/proc*/$varfile $targetdir/proc$i/"
    # Copy all files you find (not efficient on dual-CPU systems, unless
    # we deleted files after copying..)
    set dollar = '$ '		# avoid quoting problems..
    $SSH $node sh -c "for f in `ls $SCRATCH_DIR/proc*/$varfile | sed s=^.*/proc=proc=`; do cp ${dollar}f $targetdir/${dollar}f; done"
    set i = `expr $i + 1`
  end
else

  # Get size of var.dat (must exist, since run.csh only ever runs after
  # start.csh). Quite some silly hack.
  set lsout = (`ls -l "$targetdir/proc0/var.dat"`)
  set size = $lsout[5]

  while (1)
    date
    foreach node ($nodelist)
      if ($debug) echo "node=$node"
      echo '$targetdir' $targetdir
      foreach file (`$SSH $node "cd $SCRATCH_DIR; ls proc*/VAR[0-9]*"`)
	set lsout = (`$SSH $node ls -l "$SCRATCH_DIR/$file"`)
        set VARsize = $lsout[5]
        if ($VARsize >= $size) then
	  # if file is ready, copy it and delete original 
	  if ($debug) echo "Copying ${node}:$SCRATCH_DIR/$file to $targetdir/$file"
	  $SSH $node "cp $SCRATCH_DIR/$file $targetdir/proc$i/ && rm $SCRATCH_DIR/$file"
        else
	  if ($debug) echo "Size of $SCRATCH_DIR/$file < $size, so not copying yet"
        endif
      end
    end

    if (! $?varfile) then
      sleep 300			# only check every 5 minutes
    endif
  end

endif

# End of file copy-snapshots
