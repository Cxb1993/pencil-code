#!/bin/csh
#
#  $Id: new,v 1.11 2003-07-20 06:49:19 brandenb Exp $
#
#  This script uses mkdatadir which links the subdirectory data
#  to some data disk (which is machine dependent and therefore
#  you may want to adapt mkdatadir for your particular machine).
#
#  Usage:
#     new run1 run2
#
#  save current working directory and make new run directory
#  and go there.
#
set CWD=$cwd
mkdir $2
cd $2
set targetdir=$cwd
#
#  In the new run directory, call mkdatadir, and
#  write file with name of directory from which
#  the new one is generated
#
$PENCIL_HOME/bin/mkdatadir
(echo $CWD/$1; /bin/date) >>! data/new_from.dir
#
#  make src dir and go there
#
mkdir src
cd src
#
#  use *.local files from previous src directory
#  and run setup-src
#
cp $CWD/$1/src/*.local .
echo "Use *.local files from:" `pwd`; ls -l *.local
cd ..
#
#  if there exists a bin/setup-src.local file
#  it is being executed prior to setup-src
#  This gives the possibility to link your own routines
#  into src (setup-src below won't overwrite those)
#
if (-f $CWD/bin/setup-src.local) $CWD/bin/setup-src.local
#
#  continue with the usual setup procedure
#
$PENCIL_HOME/bin/setup-src
#
#  copy *.in and k.dat files into new directory
#  the k.dat file is actually only needed for forced
#  turbulence runs
#
cd $CWD/$1
echo "Use *.in files from:" `pwd`; ls -l *.in
cp *.in $targetdir
if (-f k.dat) cp k.dat $targetdir
#
