#!/bin/sh
#  -*-Perl-*-  (for Emacs)    vim:set filetype=perl:  (for vim)
#======================================================================#
# Run the right perl version:
if [ -x /usr/local/bin/perl ]; then
  perl=/usr/local/bin/perl
elif [ -x /usr/bin/perl ]; then
  perl=/usr/bin/perl
else
  perl=`which perl| sed 's/.*aliased to *//'`
fi

exec $perl -x -S $0 "$@"     # -x: start from the following line
#======================================================================#
#! /Good_Path/perl -w
# line 17

# Name:   pc_build
# Author: wd (wdobler [at] gmail [dot] com)
# Date:   23-Jun-2009
# SVN: $Id$
# Description:
#   Compile the Pencil Code, using settings from the `Makefile' section in
#   the appropriate configuration files.
# Usage:
#   pc_build [-v|-h]
#   pc_build [-f <file1,file2,...>] [-H <host_ID>]
#   pc_build [<options>] [<MAKE_VAR1=val1> [<MAKE_VAR2>=val2 ...]]
# Options:
#   -h, --help      This help.
#       --clean     Run `make clean'
#       --cleanall  Run `pc_setupsrc; make cleanall'
#   -f <files>,
#       --config-files=<files>
#                   Use the given <files> (a comma-separated list) as
#                   configuration files, rather than trying to find a
#                   config file base on a host ID.
#   -H <id>
#       --host-id=<id>
#                   Use the given <id> as host ID.
#       --notify    Tell us (audibly and visually) when compilation is done.
#       --fast      Shortcut for the option FFLAGS+='-O0'
#   -q, --quiet     Be quiet.
#   -v, --version   Print version number.
# Examples:
#   pc_build
#       use host ID mechanism to identify the appropriate config file.
#   pc_build REAL_PRECISION=double
#       run `make REAL_PRECISION=double', i.e. compile for double precision
#   pc_build -f os/GNU_Linux,mpi/open-mpi
#       will use the configuration files
#       ${PENCIL_HOME}/config/os/GNU_Linux.conf and
#       ${PENCIL_HOME}/config/mpi/open-mpi.conf (unless you have analogous
#       files under ~/.pencil/config to override the ones in
#       ${PENCIL_HOME}).
#   pc_build -f /home/USER/myconfig
#       will use the configuration file /home/USER/myconfig.conf if it
#       exists.
#   pc_build -H workhorse
#       will look for, and use the configuration file for host ID
#       `workhorse'. If no config file is found, pc_build exits with an
#       error message.

# To do / ideas:
#   - Allow make-style assignement arguments:
#       pc_build FC=myf90 FFLAGS+='-O0 -g'
#   - Allow handing down flags to `make' with
#       -Wm,<flag1>,<flag2>,...
#       --make-flags=<flag1>,<flag2>,...
#   - Allow setting the search path

# Copyright (C) 2009  Wolfgang Dobler
#
# This program is free software; you can redistribute it and/or modify it
# under the same conditions as Perl or under the GNU General Public
# License, version 3 or later.

use strict;
BEGIN {
    # Make sure ${PENCIL_HOME}/lib/perl is in the Perl path
    if (-d "$ENV{PENCIL_HOME}/lib/perl") {
        unshift @INC, "$ENV{PENCIL_HOME}/lib/perl";
    } else {
        if ($0 =~ m!(.*[/\\])!) { unshift @INC, "$1../lib/perl"; }
    }
}

use Pencil::Util;
Pencil::Util::use_pencil_perl_module('Pencil::ConfigFinder') or die;
Pencil::Util::use_pencil_perl_module('Pencil::ConfigParser') or die;

use POSIX qw(strftime tmpnam);
use Getopt::Long;
# Allow for `-Plp' as equivalent to `-P lp' etc:
Getopt::Long::config("bundling");

my (%opts);                     # Options hash for GetOptions

## Process command line
GetOptions(\%opts,
           qw( -h   --help
                    --debug
                    --clean
                    --cleanall
               -f=s --config-files=s
               -H=s --host-id=s
                    --notify
                    --fast
                    --previous-flags
               -q   --quiet
               -v   --version )
          ) or die "Aborting.\n";

my $debug = ($opts{'debug'} ? 1 : 0 ); # undocumented debug option
if ($debug) {
    printopts(\%opts);
    print "\@ARGV = `@ARGV'\n";
}

if ($opts{'h'} || $opts{'help'})    { die usage();   }
if ($opts{'v'} || $opts{'version'}) { die version(); }

my $clean        = (              $opts{'clean'}          || '');
my $cleanall     = (              $opts{'cleanall'}       || '');
my $config_files = ($opts{'f'} || $opts{'config-files'}   || undef);
my $host_id      = ($opts{'H'} || $opts{'host-id'}        || undef);
my $notify       = (              $opts{'notify'}         || '');
my $prev_flags   = (              $opts{'previous-flags'} || '');
my $quiet        = ($opts{'q'} || $opts{'quiet'}          || '');
my $fast         = (              $opts{'fast'}           || '');

my $log_file = '.pc_build_history';
if ($prev_flags) {
    system('sh', $log_file);
} else {
    log_command_line($0, \@ARGV);
}

if ($fast) {
    push @ARGV, 'FFLAGS+=-O0';
}

my @extra_make_args = @ARGV;

if ($cleanall) {
    system('pc_setupsrc');
    system('make', 'cleanall');
    exit 0;
}

if ($clean) {
    system('make', 'clean');
    exit 0;
}

my @config_files;
mention($Pencil::ConfigFinder::debug);
$Pencil::ConfigFinder::debug = 1 if ($debug);
if (defined $config_files) {
    my @files = split(",", $config_files);
    @config_files = Pencil::ConfigFinder::locate_config_files(@files) ;
} else {
    my $config_file;
    if (defined $host_id) {
        $config_file = Pencil::ConfigFinder::find_config_file_for_host($host_id);
    } else {
        $config_file = Pencil::ConfigFinder::find_config_file()
    }
    die "Fatal: Couldn't find config file.\n" unless (defined $config_file);
    push @config_files, $config_file;
    print STDERR "Found config file <$config_files[0]>\n" unless ($quiet);
}

die "No configuration file found\n" unless @config_files;
my $parser = new Pencil::ConfigParser(@config_files);
$parser->debug(1) if ($debug);

unless (-d './src/scripts') {
    print STDERR "Running pc_setupsrc\n";
    system('pc_setupsrc');
}

my @make_args = @{$parser->get_makefile_args()};
push @make_args, @extra_make_args;

# Handle MAKE_VAR1=-j4, etc.
# NOTE: This should be implemented with -Wm,<options>, akin to
# pencil-test's -Wa or gcc's -Wl options
map { s/^\s*MAKE_VAR\d*\s*=\s*// } @make_args;

print STDERR "Running make '", join("' '", @make_args), "'\n"
  unless ($quiet);

system('make', @make_args) == 0
  or die "Make failed: <$!>\n";

# ---------------------------------------------------------------------- #
sub mention {
# Reference a variable without doing anything.
# Use this to suppress ``Name "Pencil::ConfigFinder::debug" used only
# once'' warning
    my @args = @_;
}
# ---------------------------------------------------------------------- #
sub log_command_line {
# Write the current command line to log file that can be run in order to
# compile with exactly the same parameters.
    my ($cmd, $args_ref) = @_;
    my @args = quote_spaces(@$args_ref);

    my $needs_header = -e $log_file ? 0 : 1;
    my $log = comment_out_all_lines($log_file);

    if ($needs_header) {
        (my $header = <<'HERE') =~ s/^\s{8}//gm;
        # This file is automatically updated each time you run pc_build.
        # Use
        #   pc_build --previous-flags
        # or
        #   sh .pc_build_history
        # to compile with the same flags as you used the previous time.
        #
HERE
        print $log $header;
    }

    my $timestamp = POSIX::strftime("%Y-%m-%d %H:%M:%S", localtime);
    print $log "# ", $timestamp, "\n";
    print $log "$cmd @args", "\n";
}
# ---------------------------------------------------------------------- #
sub comment_out_all_lines {
# Comment out all non-empty, non-commented lines in $file.
# Return a file handle to that new version of the file.
    my ($file) = @_;

    use File::Copy;

    if (! -e $log_file) {
        open(my $log, '>', $log_file)
          or return warn_politely("Could not open $log_file for writing");
        return $log;
    }


    my $tmpfile = POSIX::tmpnam();
    unless (copy($file, $tmpfile)) {
        return '';
    }

    open(my $in, '<', $tmpfile)
      or return warn_politely("Cannot read from $tmpfile");
    open(my $out, '>', $file)
      or return warn_politely("Cannot write to $file\n");

    while (defined(my $line = <$in>)) {
        if ($line =~ /^\s*[^#\s]/) {
            $line = "# $line";
        }
        print $out $line;
    }

    close $in;

    return $out;
}
# ---------------------------------------------------------------------- #
sub warn_politely {
# Print a warning and return ''
    my ($warning) = @_;

    chomp $warning;
    warn "$warning\n";
    return '';
}
# ---------------------------------------------------------------------- #
sub quote_spaces {
# Put any string containing spaces in ''.
# This is primitive and won't help for command line arguments containing
# the single quote character.
    my @strings = @_;

    my @quoted = ();
    foreach my $string (@strings) {
        if ($string =~ /\s/) {
            $string = "'$string'";
        }
        push @quoted, $string;
    }

    return @quoted;
}
# ---------------------------------------------------------------------- #
sub printopts {
# Print command line options
    my ($optsref) = @_;
    my %opts = %$optsref;
    foreach my $opt (keys(%opts)) {
        print STDERR "\$opts{$opt} = `$opts{$opt}'\n";
    }
}
# ---------------------------------------------------------------------- #
sub usage {
# Extract description and usage information from this file's header.
    my $thisfile = __FILE__;
    local $/ = '';              # Read paragraphs
    open(FILE, "< $thisfile") or die "Cannot open $thisfile\n";
    while (<FILE>) {
        # Paragraph _must_ contain `Description:' or `Usage:'
        next unless /^\s*\#\s*(Description|Usage):/m;
        # Drop `Author:', etc. (anything before `Description:' or `Usage:')
        s/.*?\n(\s*\#\s*(Description|Usage):\s*\n.*)/$1/s;
        # Don't print comment sign:
        s/^\s*# ?//mg;
        last;                        # ignore body
    }
    return $_ or "<No usage information found>\n";
}
# ---------------------------------------------------------------------- #
sub version {
# Return CVS data and version info.
    my $doll='\$';              # Need this to trick CVS
    my $cmdname = (split('/', $0))[-1];
    my $rev = '$Revision: 1.12 $';
    my $date = '$Date: 2008/07/07 21:37:16 $';
    $rev =~ s/${doll}Revision:\s*(\S+).*/$1/;
    $date =~ s/${doll}Date:\s*(\S+).*/$1/;

    return "$cmdname version $rev ($date)\n";
}
# ---------------------------------------------------------------------- #
END {
    Pencil::Util::notify('building') if $notify;
}
# ---------------------------------------------------------------------- #

# End of file build
