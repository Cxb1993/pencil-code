#!/bin/csh
#$Id: cvsci_run,v 1.15 2004-07-26 08:13:08 mee Exp $
#
#  This script adds a new run to the repository
#  and checks in all *.in files as well as material
#  from the data and src directories.
#
#  if this procedure is called with an argument,
#  then first go into this directory and continue from there
#
if ($#argv == 1) then
  cd $1
endif
#
#  set directory
#
set CWD=$cwd
set dir=$cwd:t
#
#  go one up add add our run directory to cvs
#
cd ..
cvs add $dir
cd $dir
#
#  add directories and files to cvs
#
cvs add data src *.in k.dat
cd src
cvs add *.local
cd ..
cd data
cvs add time_series.dat index.pro dim.dat legend.dat params.log *.nml
#
#  add power spectra, if they exist
#  (for the time being, test existence only on a few)
#
\ls | egrep 'power.*\.dat' >/dev/null
if ( ! $status ) then
  cvs add power*.dat
endif
#
#  add info about directory history
#
if (-e new_from.dir) then
    cvs add new_from.dir
endif
if (-e remeshed_from.dir) then
    cvs add remeshed_from.dir
endif
#
#  check in everything
#  need to take full path name, because we are still in data,
#  which may only be a link.
#
cd $CWD
cvs ci -m "added new run: `uname -n -s`"
