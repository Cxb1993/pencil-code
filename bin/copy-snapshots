#!/bin/csh -x

# Name:   copy-snapshots
# Author: wd (Wolfgang.Dobler@kis.uni-freiburg.de)
# Date:   24-Sep-2002
# Description:
#   Copy snapshots VAR# from /scratch to $PBS_O_WORKDIR (from where the
#   code started).
#     Relies on PBS (and on tcsh)  and is needed on Horseshoe (the Odense
#   cluster).
#     Should better be a Perl script.

set debug = 0

if ($#argv > 0) then
  if (($1 == "-v") || ($1 == "--verbose")) then
    set debug = 1
    shift
  endif
endif

if ($#argv > 0) then
  set varfile = $1
endif

set targetdir = $PBS_O_WORKDIR/data
set nodelist = `cat $PBS_NODEFILE`

if ($debug) then
  echo "SCRATCH_DIR = $SCRATCH_DIR"
  echo "targetdir = $targetdir"
  echo "nodelist = $nodelist"
endif

if ($?varfile) then
  set i=0
  foreach node ($nodelist)
    if ($debug) echo "node=$node; targetdir=$targetdir"
    $SSH $node "ls -ltd $SCRATCH_DIR $SCRATCH_DIR/$varfile $targetdir/proc$i/"
    $SSH $node "cp $SCRATCH_DIR/$varfile $targetdir/proc$i/"
    set i = `expr $i + 1`
    echo 'i=' $i
  end
else

  # Get size of var.dat (must exist, since run.csh only ever runs after
  # start.csh). Quite some silly hack.
  set lsout = (`ls -l "$SCRATCH_DIR/var.dat"`)
  set size = $lsout[5]

  while (1)
    set i=0
    foreach node ($nodelist)
      if ($debug) echo "node=$node"
      echo '$targetdir' $targetdir
      foreach file (`$SSH $node "cd $SCRATCH_DIR; ls VAR[0-9]* | sort -nk1.4"`)
        set lsout = (`$SSH $node ls -l "$SCRATCH_DIR/$file"`)
        set VARsize = $lsout[5]
        if ($VARsize >= $size) then
	  # if file is ready (-Z is tcsh-specific), copy it and delete original 
	  if ($debug) echo "Copying $SCRATCH_DIR/$file to $targetdir/proc$i"
	  $SSH $node "cp $SCRATCH_DIR/$file $targetdir/proc$i/ && rm $SCRATCH_DIR/$file"
        else
	  if ($debug) echo "Size of $SCRATCH_DIR/$file < $size, so not copying yet"
        endif
      end
      set i = `expr $i + 1`
      echo 'i=' $i
    end

    #sleep 300			# only check every 5 minutes
    sleep 10			# only check every 5 minutes

  end

endif

# End of file copy-snapshots
