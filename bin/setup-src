#!/bin/csh
#                       setup-src
#                      -----------
# Description:
#   Link files into a local src directory
#   Also link files from the bin directory ==> should be renamed
# CVS $Id: setup-src,v 1.24 2005-06-26 17:34:17 eos_merger_tony Exp $
# Usage:
#   setup-src  [src-dir]

if (! $?PENCIL_HOME) then
  echo "You need to set PENCIL_HOME; consider sourcing sourceme.{,c}sh"
  exit 0
endif

set src = ./src
if ($#argv > 0) set src = "$1"

#  ## Determine directories to link from
#  set bindir="No-such-directory"
#  set srcdir="No-such-directory"
#  foreach dir ( ../../.. ../..)
#    # Note: the _last_ matching directory will be used
#    if (-d $dir/bin) set bindir=$dir/bin
#    if (-d $dir/src) set srcdir=$dir/src
#  end
set bindir = $PENCIL_HOME/bin
set srcdir = $PENCIL_HOME/src

## Link csh scripts
foreach script (start.csh start_run.csh run.csh getconf.csh)
  if (-e $bindir/$script) then
    test -h ./$script 
    if ( ! $status ) then 
#      echo Removing - $script
      \rm -f $script
    endif
    if ( ! -e ./$script ) ln -fs $bindir/$script .
  else
    echo "No such file: $bindir/$script"
  endif
end

## Make a link to a root Makefile to allow make from the run directory
test -h Makefile
if ( ! $status )  then
#  echo Removing - Makefile
  \rm -f Makefile
endif
ln -s $srcdir/Makefile.parent Makefile

## src subdirectory
if (! -d $src) then
  echo "WARNING: No src directory found -- creating new one"
  mkdir $src
  
endif
#
cd $src || exit 1
#
#  LINK all *.f90, *.f, and the Makefile to local src directory
#
set n=0
foreach file ($srcdir/*.f90 $srcdir/*.f $srcdir/*.c \
              $srcdir/Makefile.src $srcdir/.cvsignore $srcdir/*.inc $srcdir/scripts $srcdir/shock_profiles $srcdir/special )
  if (-e $file) then
    if (! -e $file:t) then
      ln -sf $file .
    else
      test -h $file:t
      if ( ! $status ) then
#        echo Removing - $file:t
        \rm -f $file:t
        ln -s $file .
      else
        # echo "$file:t already exists"
        set n=`expr $n + 1`
        set existf=$file:t
      endif
    endif
  else
    echo "No such file: $file"
  endif
end
#
#  COPY all *.local files into local src directory
#
set n=0
foreach file ($srcdir/*.local)
  if (-e $file) then
    if (! -e $file:t) then
      cp $file .
    else
      # echo "$file:t already exists"
      set n=`expr $n + 1`
      set existf=$file:t
    endif
  else
    echo "No such file: $file"
  endif
end
#
if ($n == 1) then
  echo "$existf already exists in $src"
endif
if ($n > 1) echo "$n files already exist in $src"
#
#  Link to global COLOR file present in $PENCIL_HOME.
#
if (-e $PENCIL_HOME/COLOR) ln -s $PENCIL_HOME/COLOR ../
#
#  Generate Makefile
#
set echo
#set verbose

\rm -f Makefile			# for old run directories
$srcdir/scripts/adapt-mkfile Makefile.src Makefile
\rm -f makefile			# for old run directories
