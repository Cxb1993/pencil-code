pro rvid_box,field,mpeg=mpeg,tmin=tmin,tmax=tmax,wait=wait,amax=amax,amin=amin,$
  nrepeat=nrepeat,datadir=datadir
;
; $Id: rvid_box.pro,v 1.3 2002-11-14 15:00:01 brandenb Exp $
;
;  Reads in 4 slices as they are generated by the pencil code.
;  The variable "field" can be changed. Default is 'lnrho'.
;
;  if the keyword /mpeg is given, the file movie.mpg is written.
;  tmin is the time after which data are written
;  nrepeat is the number of repeated images (to slow down movie)
;
;  Typical calling sequence
;  rvid_box,'bz',tmin=190,tmax=200,amin=-.35,amax=.35,/mpeg
;
default,amax,.05
default,amin,-amax
default,field,'lnrho'
default,datadir,'data'
default,nrepeat,0
default,tmin,0.
default,tmax,1e38
default,wait,.03
;
file_slice1=datadir+'/slice_'+field+'.Xy'
file_slice2=datadir+'/slice_'+field+'.xy'
file_slice3=datadir+'/slice_'+field+'.xz'
file_slice4=datadir+'/slice_'+field+'.yz'
;
;  Read the dimensions and precision (single or double) from dim.dat
;
mx=0L & my=0L & mz=0L & nvar=0L & prec=''
nghostx=0L & nghosty=0L & nghostz=0L
;
close,1
openr,1,datadir+'/'+'dim.dat'
readf,1,mx,my,mz,nvar
readf,1,prec
readf,1,nghostx,nghosty,nghostz
close,1
;
nx=mx-2*nghostx
ny=my-2*nghosty
nz=mz-2*nghostz
;
t=0. & islice=0
xy2=fltarr(nx,ny)
xy=fltarr(nx,ny)
xz=fltarr(nx,nz)
yz=fltarr(ny,nz)
;
;  open MPEG file, if keyword is set
;
if keyword_set(mpeg) then begin
  Nwx=400 & Nwy=320
  if (!d.name eq 'X') then window,2,xs=Nwx,ys=Nwy
  mpeg_name = 'movie.mpg'
  print,'write mpeg movie: ',mpeg_name
  mpegID = mpeg_open([Nwx,Nwy],FILENAME=mpeg_name)
  itmpeg=0 ;(image counter)
end
;
close,1 & openr,1,file_slice1,/f77
close,2 & openr,2,file_slice2,/f77
close,3 & openr,3,file_slice3,/f77
close,4 & openr,4,file_slice4,/f77
while not eof(1) do begin
  readu,1,xy2,t
  readu,2,xy,t
  readu,3,xz,t
  readu,4,yz,t
  if t ge tmin and t le tmax then begin
    boxbotex_scl,xy2,xy,xz,yz,1.,1.,zof=.7,zpos=.34,ip=3,amin=amin,amax=amax
    xyouts,.93,1.13,'!8t!6='+string(t,fo="(f6.1)"),col=1,siz=2
    if keyword_set(mpeg) then begin
      image = tvrd(true=1)
      for irepeat=0,nrepeat do begin
        mpeg_put, mpegID, window=2, FRAME=itmpeg, /ORDER
        itmpeg=itmpeg+1 ;(counter)
      end
      print,islice,itmpeg,t,min([xy2,xy,xz,yz]),max([xy2,xy,xz,yz])
    end else begin
      print,islice,t,min([xy2,xy,xz,yz]),max([xy2,xy,xz,yz])
    end
    wait,wait
  end
  islice=islice+1
end
close,1
close,2
close,3
close,4
;
;  write & close mpeg file
;
if keyword_set(mpeg) then begin
  print,'Writing MPEG file..'
  mpeg_save, mpegID, FILENAME=mpeg_name
  mpeg_close, mpegID
end
;
END
